buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.1'
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.application'

def properties = new Properties();
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def ovr_openxr_mobile_sdk_dir = properties.getProperty('ovr_openxr_mobile_sdk.dir')
def gstreamer_sdk_dir = properties.getProperty('gstreamer_sdk.dir')

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.3"
    ndkVersion "21.4.7075529"

    defaultConfig {
        applicationId "cz.but.telepresence"
        // for Vulkan, need at least 24
        minSdkVersion 24
        compileSdkVersion 26
        namespace "cz.but.telepresence"

        versionName = "1.0"
        versionCode = 1

        externalNativeBuild {
            ndk {
                abiFilters 'arm64-v8a'
            }
            cmake {
                arguments "-DOVR_OPENXR_MOBILE_SDK_DIR:STRING=${ovr_openxr_mobile_sdk_dir}"
                arguments "-DGSTREAMER_SDK_DIR:STRING=${gstreamer_sdk_dir}"
                cppFlags ''
            }
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDir 'android_resources'
        }
        Vulkan {
            res.srcDir 'android_resources/vulkan'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    externalNativeBuild {
        cmake {
            path file("CMakeLists.txt")
        }
    }

    flavorDimensions 'api'

    productFlavors {
        Vulkan {
            dimension 'api'
            applicationIdSuffix '.vulkan'
            externalNativeBuild.cmake.cppFlags '-DDEFAULT_GRAPHICS_PLUGIN_VULKAN'

            resValue "string", "app_name", "BUT Telepresence (Vulkan)"
        }
    }
}

